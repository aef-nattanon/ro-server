//===== Hercules Script ======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.7
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch(getarg(0)){
		default:
		case 0:
			// Default message (obsolete)
			mes "[Kafra Employee]";
			mes "ยินดีต้อนรับสู่";
			mes "Kafra Corporation.";
			mes "บริการ Kafras";
			mes "จะอยู่เคียงข้างคุณ";
			mes "มีอะไรให้ฉันช่วย?";
			break;
		case 1:
			// Niflheim specific message
			mes "[Kafra Employee]";
			mes "^666666ย-ยิน-น-ดีต้อนรับ";
			mes "สู่ บริการ- K-kaaafrrrra";
			mes "C-coorpoor-r-ratioooonn...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			set @GID,getcharid(2);
			mes "[Kafra Employee]";
	 		mes "ยินดีต้อนรับสมาชิกกิล ^ff0000" + getguildname(@GID) + "^000000";
			mes "พวกเราจะอยู่เคียงข้างคุณเสมอ";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[Kafra Employee]";
			mes "คุณคงมาจากดินแหนห่างไกล คงจะมาเพื่อเรียนวัฒนธรรมจากที่นี่ หรือไม่ก็ท่องเที่ยวสินะ?";
			mes "ไม่ว่าจะเพราะอะไร ทำไมคุณไม่ลองพักอยู่ที่นี่ล่ะ?";
			mes "อากาศและกลิ่มหอมของดอกไม้ที่นี่น่าช่างรื่นรมย์";
			break;		
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[Kafra Employee]";
			mes "บริการ Kafras ของพวกเรา";
			mes "ไม่ว่าคุณจะอยู่ไกลแค่ไหน เราจะอยู่ข้างคุณเสมอ";
			break;		
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (1) {
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"เก็บของ","เก็บของกิล","เช่ารถเข็น","ใช้บริการ Teleport","ยกเลิก";
		} else {
			switch(getarg(1)){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"เซฟ","เก็บของ","ยกเลิก"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"เก็บของ","ยกเลิก"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"เซฟ","เก็บของ","เช่ารถเข็น","ตรวจสอบข้อมูลอื่น","ยกเลิก"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"เก็บของ","เช่ารถเข็น","ตรวจสอบข้อมูลอื่น","ยกเลิก"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"เก็บของ","ตรวจสอบข้อมูลอื่น","ยกเลิก"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"เซฟ","เก็บของ","เช่ารถเข็น","ยกเลิก"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"เซฟ","เก็บของ","ตรวจสอบข้อมูลอื่น","ยกเลิก"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"เก็บของ","เช่ารถเข็น","ใช้บริการ Teleport","ตรวจสอบข้อมูลอื่น","ยกเลิก"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"เก็บของ","เซฟ","เช่ารถเข็น","ยกเลิก"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"เซฟ","เก็บของ","ใช้บริการ Teleport","เช่ารถเข็น","ตรวจสอบข้อมูลอื่น","ยกเลิก"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "เซฟ") {
			return;
		} else if (@K_Menu0$[.@j] == "เก็บของ") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),getarg(0);
			next;
		} else if (@K_Menu0$[.@j] == "ใช้บริการ Teleport") {
			// Display Einbroch "No Teleport Service" notice.
			if(getarg(1) != 4) callfunc "F_KafTele",getarg(0);
			else {
				mes "[Kafra Employee]";
				mes "การบริการ Teleport ของเรามี ^FF0000ข้อจำกัด";
				mes "จากการตกลงในการขนส่ง^000000";
				mes "โดย Kafra Corporation ไม่สามารถ";
				mes "พาคุณ Teleport ไปยัง";
				mes "รัฐ Schwaltzvalt ได้";
				next;
				mes "[Kafra Employee]";
				mes "คุณควรใช้บริการ";
				mes "Airship แทน";
				mes "ขอขอบคุณที่เข้าใจ";
				mes "และให้ความร่วมมือ";
			}
			next;
		} else if (@K_Menu0$[.@j] == "เช่ารถเข็น") {
			if(callfunc("F_KafCart",getarg(0),getarg(4)) == 1) 
				next;
		} else if (@K_Menu0$[.@j] == "ตรวจสอบข้อมูลอื่น") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "ยกเลิก"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		} else if (@K_Menu0$[.@j] == "เก็บของกิล") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if(getarg(0) == 1){
		if(guildopenstorage()){
			mes "[Kafra Employee]";
			mes "ขออภัย มีสมาชิกท่านอื่นใช้บริการอยู่";
			mes "โปรดลองใหม่ภายหลัง";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
		mes "[Kafra Employee]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666ข-อ-อ-อ อภัย";
			mes "ท่าน-น ต้องมีทักษะ Level 6 6...^000000";
			return;
		}
		mes "ไม่สามารถให้บริการได้";
		mes "เนื่องจากท่านต้องมีทักษะพื้นฐาน Level 6";
		mes "ในการใช้บริการเก็บของ";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(getarg(0) != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "^666666เงินนนนน...";
					mes "ข-ข้าต้องการ 150... Zeny!";
					return;
				}
				// Standard Message
				mes "ขออภัย";
				mes "จำนวนเงินของท่านไม่เพียงพอ";
				mes "สำหรับใช้บริการ";
				mes "ต้องการเงินจำนวน "+getarg(1)+" zeny.";
				return;
			}
			Zeny -= getarg(1);
			set RESRVPTS, RESRVPTS + (getarg(1)/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Kafra Employee]";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666ขอบคุณ.. ที่ใช้... บริการ...^000000";
		}
		// Normal message
		else {
			mes "[Kafra Employee]";
			mes "บริการเก็บของ";
			mes "นี่คือห้องเก็บของสำหรับคุณ";
			mes "ขอบคุณที่ใช้บริการ";
			mes "Kafra Service.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Kafra Employee]";
	mes "โปรดเลือก";
	mes "จุดหมายปลายทาง";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "ยกเลิก") return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "[Kafra Employee]";
			mes "ขออภัยไม่สามารถให้บริการได้";
			mes "เนื่องจากท่านมีเงินไม่เพียงพอ";
			mes "สำหรับค่าบริการในการ Teleport";
			mes "ไป "+@wrpD$[.@j]+" ราคา "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		Zeny -= @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Pharos Beacon") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mjolnir Dead Pit") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Orc Dungeon") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 78, 104;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	// Custom
	// ----------- Ayothaya
	else if (@wrpD$[.@j] == "Ayothaya ดันเจี้ยนชัั้นที่ 1") warp "ayo_dun01", 275, 19;
	else if (@wrpD$[.@j] == "Ayothaya ดันเจี้ยนชัั้นที่ 2") warp "ayo_dun02", 24, 26;
	// ----------- Lighthalzen
	else if (@wrpD$[.@j] == "Bio Labs ชัั้นที่ 1") warp "lhz_dun01", 150, 228;
	else if (@wrpD$[.@j] == "Bio Labs ชัั้นที่ 2") warp "lhz_dun02", 150, 18;
	else if (@wrpD$[.@j] == "Bio Labs ชัั้นที่ 3") warp "lhz_dun03", 140, 134;
	//else if (@wrpD$[.@j] == "Bio Labs ชัั้นที่ 4") warp "lhz_dun04", 24, 26;
	// ----------- Juperos Ruins
	else if (@wrpD$[.@j] == "Juperos Ruins ชัั้นที่ 1") warp "juperos_01", 53, 247;
	else if (@wrpD$[.@j] == "Juperos Ruins ชัั้นที่ 2") warp "juperos_02", 37, 63;
	else if (@wrpD$[.@j] == "Juperos Core") warp "jupe_core", 150, 285;
	// ----------- Kiel Robot Factory
	else if (@wrpD$[.@j] == "Kiel Robot Factory ชั้นที่ 1") warp "kh_dun01", 28, 226;
	else if (@wrpD$[.@j] == "Kiel Robot Factory ชั้นที่ 2") warp "kh_dun02", 41, 198;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "[Kafra Employee]";
		mes "ขออภัย";
		mes "บริการเช่ารถเข็น";
		mes "จะใช้ได้เฉพาะกับอาชิพ,";
		mes "พ่อค้าเท่านั้น";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "[Kafra Employee]";
		mes "ท่านมีรถเข็นอยู่แล้ว";
		mes "และต้องการเพิ่ม";
		mes "เราไม่สามารถให้ท่านได้";
		mes "มากกว่าหนึ่ง";
		mes "โปรดมาใหม่ภายหลัง";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "[Kafra Employee]";
		mes "บริการเช่ารถเข็น";
		mes "ค่าบริการ FREE";
		mes "ท่านต้องการเช่ารถเข็นใช่หรือไม่?";
		next;
		if (select(" เช่ารถเข็น: ยกเลิก") == 2) return 0;
			if(Zeny<getarg(1)){
				mes "[Kafra Employee]";
				mes "ขออภัย";
				mes "จำนวนเงินของท่านไม่เพียงพอ";
				mes "ในการใช้บริการเช่ารถเข็น";
				mes "ต้องการเงินจำนวน "+getarg(1)+" zeny.";
				return 1;
			}
			//Zeny -= getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "ตรวจสอบบริการพิเศษ","ใส่รหัส คลังเก็บของ","หาตำแหน่ง Kafra","ยกเลิก";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "Check Special Reserve Points.") {
			mes "[Kafra Employee]";
			mes "คุณ...";
			mes strcharinfo(0) + "...";
			mes "ท่านมีคะแนนสะสม";
			mes RESRVPTS+ " Points.";
			next;
			mes "[Kafra Employee]";
			mes "ท่านสามารถนำใบเสร็จไปแลกของรางวัล";
			mes "เล็กๆน้อยๆ";
			mes "ได้ที่เมือง Al De Baran.";
			mes "ขอบคุณที่ใช้บริการค่ะ";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "หาตำแหน่ง Kafra") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "ใส่รหัส คลังเก็บของ") {
			callfunc("F_SetKafCode","[Kafra Employee]","Kafra Services");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "[Kafra Employee]";
	if(getarg(1)==1) {	
		// Save
		mes "ตำแหน่งของท่าน";
		mes "ได้ถูกบันทึกไว้แล้ว";
		mes getarg(2)+".";
		mes "ขอบคุณที่ใช้บริการ";
		mes "Kafra Services.";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "พวกเรา Kafra Corporation,";
		mes "ขอขอบคุณทุกท่านที่ใช้บริการ  พวกเราจะบริการทุกท่านอย่างเต็มที่เพื่อความพึงพอใจสูงสุด";
	} else if(getarg(0)==1) {
		// Niflheim End
		percentheal 0,- 25;
		mes "^666666พ-พ-พวกเรา Kaffffra";
		mes "จะไม่ลืมท่าน...";
		mes "จ-จ-จนวัน... ต-ต-ตายย~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "โปรดใส่รหัสคลังที่ต้องการ:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "รหัสผิดพลาด";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	mes getarg(0);
	if(#kafra_code) {
		mes "คลังเก็บของถูกป้องกันด้วยรหัสลับ ท่านต้องการทำสิ่งใด?";
		next;
		switch (select("เปลี่ยนรหัสผ่าน -> 5000z:ยกเลิกรหัสผ่าน -> 1000z:ยกเลิก")) {
			case 1:
				mes getarg(0);
				mes "โปรดใส่ ^0000FFรหัสผ่าน^000000 เข้าคลังของท่าน";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					dispbottom "รหัสผ่านของท่านผิดพลาด";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			case 2:
				mes getarg(0);
				mes "โปรดใส่รหัสผ่านของท่าน เพื่อยกเลิกการใช้งาน";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					dispbottom "รหัสผ่านของท่านผิดพลาด";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if(Zeny < 1000) {
					mes "ขออภัยด้วย แต่จำนวนเงินของท่านไม่เพียงพอ";
					emotion e_cash;
					break;
				}
				Zeny -= 1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "ทำรายการเรียบร้อบ.";
					mes "ขอบคุณที่ใช้บริการค่ะ"+getarg(1)+"";
					emotion e_thx;
				} else {
					mes "รหัสผ่านผิดพลาดเราจะไม่คืนเงิน 1000z ให้กับคุณ";
					mes "โปรดใส่รหัสผ่านให้ถูกต้องอีกครั้ง";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes getarg(1)+" เราขอเสนอบริการใหม่ของเรา :";
		mes "ท่านจะสามารถปกป้องคลังของท่านได้ ด้วยรหัสผ่าน";
		next;
		switch (select("ตั้งรหัสผ่านใหม่ -> 5000z:ยกเลิก")) {
			case 1: callsub S_SET,getarg(0),getarg(1); break;
			case 2: break;		
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "ใส่ ^FF0000รหัสผ่านใหม่^000000 ที่ท่านต้องการ เพื่อปกป้องการใช้งานคลัง";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "ท่านทำรายการไม่สำเร็จ โปรดลองอีกครั้งค่ะ";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if(Zeny < 5000) {
		mes "จำนวนเงินของท่านไม่เพียงพอ";
		emotion e_cash;
		return;
	}
	Zeny -= 5000;
	set #kafra_code,@code;
	mes "คลังของท่านถูกปกป้องด้วยรหัสผ่านแล้ว";
	mes "ขอบคุณที่ใช้บริการค่ะ "+getarg(1)+"";
	emotion e_thx;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Enter a number 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: มีผู้ไม่ประสงค์ดีกำลังพยายามเข้าสู่คลังของท่าน";
	}
	if(input(@code_) == 1) {
		mes "รหัสผ่านของท่านเกินจำนวนที่กำหนด";
		return 0;
	}
	if(@code_<1000) {
		mes "รหัสผ่านของท่านสั้นเกินไป";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Orc Dungeon", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1200, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mjolnir Dead Pit";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1800, 1200;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Orc Dungeon", "Mjolnir Dead Pit";
		setarray @wrpP[0], 1200, 1200, 1200, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Pharos Beacon";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}
	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]");
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","ยกเลิก";
	return;
}
